static x;
static y;

y = 0;
forall (x in [array 4, 5, 6])
    y += x;
if (y != 15)
    fail("forall loop gave wrong sum");

y = 0;
forall (x in NULL)
    y += x;
if (y != 0)
    fail("NULL forall loop failed");

y = 0;
for (x = 1; x < 10; ++x)
	y += x;
if (y != 45)
	fail("for loop executed wrong number of times");

y = 0;
for (x = 1; x < 10; ++x)
{
	if (x & 1)
		continue;
	y += x;
}
if (y != 20)
	fail("continue executed incorrectly");

y = 0;
for (x = 1; x < 10; ++x)
{
	critsect
	{
		if (x & 1)
			continue;
	}
	y += x;
}
if (y != 20)
	fail("continue in critsect executed incorrectly");

y = 0;
for (x = 1; x < 10; ++x)
{
	critsect
	{
		if (x == 3)
			break;
	}
	y += x;
}
if (y != 3)
	fail("break in critsect executed incorrectly");

// Test use of 'break' outside a loop or switch.
// Since this is a parse-time error rather than an execution-time error,
// 'onerror' handlers are not in effect, so the naive approach:
//   try { break; } onerror { yay, it failed correctly! }
// won't work (ici will just stop parsing our file).  So to run this test, we
// need to use parse(), whose runtime failure we can capture.
forall (str in [array
    "break;",
    "static foo() { break; }",
    "static foo() { try { break; } onerror; }", // Can't break from a 'try'
    "static foo() { critsect { break; } }", // Can't break from a 'critsect'
])
{
    error = NULL;
    try
        parse(str);
    onerror
	;
    if (error !~ #break outside of breakable statement#)
	fail("break outside of breakable statement didn't fail");
}

// Test use of 'continue' outside a loop.  The above comment applies
// here too, so we put the continue in its own function.
forall (str in [array
    "continue;",
    "static foo() { continue; }",
    "static foo() { try { continue; } onerror; }",
    "static foo() { critsect { continue; } }",
    "static foo() { switch(0) { case 0: continue; } }",
])
{
    error = NULL;
    try
        parse(str);
    onerror
	;
    if (error !~ #continue outside of continueable statement#)
        fail("continue outside of continueable statement didn't fail");
}

x = 0;
switch("a")
{
	x = 1;
}
if (x != 0)
	fail("incorrect switch execution");

x = 0;
switch("a")
{
default:
	x = 1;
}
if (x != 1)
	fail("incorrect switch execution");

	
x = 0;
switch("a")
{
case "a":
	x = 1;
	break;

case "b":
	x = 2;
}
if (x != 1)
	fail("failed to switch correctly");


error := NULL;
try
	X := totaly_undefined_name;
onerror
	;

if (error == NULL)
	fail("didn't fail on undefined name");

static
load(name)
{
	return super(scope()).auto_defined_global = 123;
}

x := auto_defined_global;
if (x != 123)
	fail("failed to auto-load global");


static
load(name)
{
}

error = NULL;
try
	x := not_auto_defined_global;
onerror
	;
if (error == NULL)
	fail("didn't fail on non auto load");


/*
 * This is actually a test of the error and recover from excessive
 * core engine recursion.
 */
static
load(name)
{
	return oh_bugger_another_undefined;
}
error = NULL;
try
	x := an_undefined_variable;
onerror
	;

if (error == NULL || error !~ #excessive recursive#)
	fail("wrong error from recursive undefined: " + error);

error := NULL;
try
	forall (x in 2)
		;
onerror
	;
if (error == NULL)
	fail("failed to fail on bad forall");


static c = [class

	method()
	{
		return 123;
	}
];

static d = [class:c,

	method()
	{
		x := this:^method;
		return this:^method() + x();
		
	}
];

x := d:new();
if (x:method() != 123 + 123)
	fail(":^ didn't work");

y = x:method;
if (y() != 123 + 123)
	fail("unoptimised method call didn't work");


/*
 * Remove our local load function.
 */
del(super(scope()), "load");
saved_load := load;
for (s := scope(); s != NULL; s = super(s))
	del(s, "load");
error = NULL;
try
	x:= an_undefined_var_with_no_load;
onerror
	;
if (error == NULL)
	fail("failed to fail on undefined with no load()");

extern load = saved_load;

auto a;
forall (a in [array 1, 2, 3])
{
	if (a == 2)
		break;
}
if (a != 2)
	fail("break from froall didn't work");
